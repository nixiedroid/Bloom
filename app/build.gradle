//file:noinspection GroovyAssignabilityCheck
plugins {
    id 'com.android.application'
}


android {
    namespace 'com.nixiedroid.bloomlwp'
    compileSdk 34

    defaultConfig {
        applicationId "com.nixiedroid.bloomlwp"
        minSdk 24
        targetSdk 34
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        buildConfigField 'boolean', 'ENABLE_CRASHLYTICS', 'true'
    }

    buildTypes {
        release {
            minifyEnabled true
            debuggable false
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        benchmark {
            initWith release
            signingConfig signingConfigs.debug
            matchingFallbacks = ['release']
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    }

tasks.register("startCmdline",Exec) {
    group = 'Start'
    commandLine 'adb', 'shell', "am start " +
            "-S -a android.service.wallpaper.CHANGE_LIVE_WALLPAPER " +
            "--ecn android.service.wallpaper.extra.LIVE_WALLPAPER_COMPONENT " +
            "com.nixiedroid.bloomlwp/.wallpaper.bloom.BloomWallpaperService"
}

tasks.register("startDebug") {
    dependsOn 'installDebug'
    group = 'Start'
    finalizedBy startCmdline
}
tasks.register("startRelease") {
    dependsOn 'installRelease'
    group = 'Start'
    finalizedBy startCmdline
}



dependencies {
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.12'
    implementation("org.greenrobot:eventbus:3.3.1")
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.gms:play-services-location:21.0.1'
    implementation "androidx.preference:preference:1.2.1"
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}